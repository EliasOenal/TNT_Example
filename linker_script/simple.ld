/*
Thumb2 Newlib Toolchain example project
Written by Elias Ã–nal <EliasOenal@gmail.com>, released as public domain.

This is a minimal linkerscript that should be easy to port to new microcontrollers.
At this point it supports only C and will lead to weird behavior when used for C++.
*/

ENTRY(Reset_Handler)

MEMORY
{ 
  RAM (xrw) : ORIGIN = 0x20000000, LENGTH = 16K
  FLASH (rx) : ORIGIN = 0x8000000, LENGTH = 64K
}

_estack = 0x20004000; /* Upper stack boundary */
_sstack = (_estack - 1024); /* Lower stack boundary */

SECTIONS {
	.isr_vector : /* interrupt vector */
	{
		. = ALIGN(4);
        	KEEP(*(.isr_vector))
		. = ALIGN(4);
	} >FLASH

	.text : /* code goes here*/
	{
		. = ALIGN(4);
		*(.text)
		*(.text.*)
		. = ALIGN(4);
   	 	_etext = .;
	} > FLASH

	.rodata : /* read only data */
	{
		. = ALIGN(4);
		*(.rodata)
		*(.rodata.*)
		. = ALIGN(4);
   	 	_sidata = .;
	} > FLASH

	.data : AT ( _sidata ) /* initial .data values go to _sidata */
	{
		. = ALIGN(4);
        	_sdata = . ;
        	_data = . ;
		*(.data)
		*(.data.*)
		. = ALIGN(4);
		_edata = . ;
	} >RAM

	.bss : /* zero initialized memory */
	{
		. = ALIGN(4);
		_sbss = .;
		_bss = .;
		*(.bss)
		*(.bss.*)
		. = ALIGN(4);
   	 	_ebss = . ;
	} >RAM

	/DISCARD/ : /* discard debug informations generated by some toolchains */
	{
		*(.ARM.*)
		*(.comment)
	}
}

. = ALIGN(4);
end = .;
_end = .;
